plugins {
    id 'com.android.application'
}

android {
    compileSdk 33
    ndkVersion "25.1.8937393"

    defaultConfig {
        applicationId "org.love2d.android"
        versionCode 31
        versionName "12.0"
        minSdk 21
        targetSdk 33
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
                // Transitive shared library dependency is not taken into account, this
                // will cause liboboe.so not get included. love depends on OpenAL that
                // depends on oboe::oboe. So, add "OpenAL" target.
                targets "love", "OpenAL"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        prefab true
    }

    flavorDimensions 'mode', 'recording'
    productFlavors {
        normal {
            dimension 'mode'
        }
        embed {
            dimension 'mode'
        }
        record {
            dimension 'recording'
        }
        noRecord {
            dimension 'recording'
        }
    }

    sourceSets {
        main {
            java {
                srcDir 'src/main/cpp/megasource/libs/SDL2/android-project/app/src/main/java'
                srcDir 'src/main/java'
                srcDir 'src/main/cpp/love/src/libraries/luahttps/src/android/java'
            }
        }
        normal {
            java {
                srcDir 'src/normal/java'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            // '+' notation apparently has been supported long time ago
            // https://issuetracker.google.com/issues/110693527#comment22
            // We require CMake 3.21 because r23 has important fixes
            // that's only fixed if CMake 3.21 is used.
            // https://github.com/android/ndk/issues/463
            version '3.21.0+'
        }
    }
    packagingOptions {
        // Due to inclusion of "OpenAL" target, these files are included too, but this
        // file is provided by Android as public API. Exclude them!
        exclude 'lib/armeabi-v7a/libOpenSLES.so'
        exclude 'lib/arm64-v8a/libOpenSLES.so'
        exclude 'lib/x86/libOpenSLES.so'
        exclude 'lib/x86_64/libOpenSLES.so'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.oboe:oboe:1.6.1'
}
