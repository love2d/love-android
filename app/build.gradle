import java.nio.charset.StandardCharsets

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

//ËØªÂèñkeystore.propertiesËé∑ÂèñÈÖçÁΩÆ‰ø°ÊÅØ
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace "org.love2d.android.executable"
    ndkVersion '25.2.9519653'

    defaultConfig {
        applicationId project.properties["app.application_id"]
        versionCode project.properties["app.version_code"].toInteger()
        versionName project.properties["app.version_name"]
        minSdk 21
        compileSdk 34
        targetSdk 34
        
        def getAppName = {
            def nameArray = project.properties["app.name_byte_array"]
            def name = project.properties["app.name"]
            if (name != null && nameArray != null) {
                throw new Exception("Only define either `app.name` or `app.name_byte_array` in gradle.properties, but not both!")
            }

            if (name == null) {
                def nameArraySplit = nameArray.split(",")
                def nameBytes = new byte[nameArraySplit.length]
                def count = 0
                for (String s: nameArraySplit) {
                    nameBytes[count++] = (byte) Integer.parseInt(s)
                }
                return new String(nameBytes, StandardCharsets.UTF_8)
            }
            return name
        }

        manifestPlaceholders = [
            NAME:getAppName(),
            ORIENTATION:project.properties["app.orientation"],
        ]
    }
    //Á≠æÂêçÈÖçÁΩÆ
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        prefab true
        viewBinding true
        compose true
    }

    flavorDimensions = ['mode', 'recording']
    productFlavors {
        normal {
            dimension 'mode'
        }
        embed {
            dimension 'mode'
        }
        record {
            dimension 'recording'
        }
        noRecord {
            dimension 'recording'
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.9"
    }

    lint {
        abortOnError false
    }
}

dependencies {
    api 'androidx.multidex:multidex:2.0.1'
    api fileTree(dir: 'libs', include: ['*.jar'])
    api project(':love')

    //‰æùËµñroomlib
    // üëá ËøôÈáå‰πüÂøÖÈ°ªÂä† Room ÁöÑ‰æùËµñ
    def room_version = "2.6.1" // Êàñ‰Ω†‰ΩøÁî®ÁöÑÁâàÊú¨
    api "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    api "androidx.room:room-ktx:$room_version"
}
